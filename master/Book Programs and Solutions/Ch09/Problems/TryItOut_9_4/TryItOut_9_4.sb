' TryItOut_9_4.sb
' Tests your knowledge of the Cartesian coordinate system

GraphicsWindow.Title = "Guess My Coordinates"
GraphicsWindow.CanResize = "False"
GraphicsWindow.Width = 480                ' Same as background image
GraphicsWindow.Height = 360               ' Same as background image
GraphicsWindow.Top = 200                  ' Position within desktop
GraphicsWindow.Left = 50                  ' Position within desktop
TextWindow.Title = "Guess My Coordinates"
TextWindow.Top = GraphicsWindow.Top 
TextWindow.Left = GraphicsWindow.Left + GraphicsWindow.Width + 15

path = Program.Directory                     ' Program's directory
bkgnd = Shapes.AddImage(path + "\Grid.png")  ' Background (480 x 360)
star = Shapes.AddImage(path + "\Star.png")   ' Star image (24 x 24)

While ("True")                               ' Runs forever
  X0 = Math.GetRandomNumber(23) - 12         ' From -11 to 11
  Y0 = Math.GetRandomNumber(17) - 9          ' From -8 to 8
  pt = "(" + X0 + ", " + Y0 + ")"            ' Example: (5, -3)
  
  ' Find star's position (in pixels) and move it
  xPos = ((X0 + 12 ) * 20) - 12              ' Additional 12 pixels to the left
  yPos = ((9 - Y0 ) * 20) - 12               ' And 12 pixels up  
  Shapes.Move(star, xPos, yPos )             ' Moves the Star  
  
  TextWindow.Write("What is the x-coordinate? ")
  xAns = TextWindow.ReadNumber()
  
  TextWindow.Write("What is the y-coordinate? ")
  yAns = TextWindow.ReadNumber()
  
  If ((xAns = X0) And (yAns = Y0)) Then      ' Player guessed correctly
    TextWindow.WriteLine("Good job! You're a star!")      
  Else                      ' Player entered an incorrect y-coordinate
    TextWindow.WriteLine("Sorry. This star is at " + pt)
  EndIf    
  
  TextWindow.WriteLine("")                   ' Empties line before new round
EndWhile