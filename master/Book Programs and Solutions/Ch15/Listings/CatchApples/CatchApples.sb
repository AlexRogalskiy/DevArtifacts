' CatchApples.sb
GraphicsWindow.Title      = "Catch Apples"
GraphicsWindow.CanResize  = "False"
GraphicsWindow.Width      = 480
GraphicsWindow.Height     = 360
GraphicsWindow.FontSize   = 14
GraphicsWindow.BrushColor = "Black"

path = Program.Directory
GraphicsWindow.DrawImage(path + "\Background.png", 0, 0 )

msgID = Shapes.AddText("")
Shapes.Move(msgID, 240, 0)

MAX_APPLES = 5     ' Change this to have more apples
AddApples()        ' Creates the apple array

cartImg  = Shapes.AddImage(path + "\Cart.png")  ' 100x80 pixels

numMissed = 0      ' Missed Apples
numCaught = 0      ' Caught Apples

While ("True")
  Shapes.Move(cartImg, GraphicsWindow.MouseX - 50, 280)
  MoveApples()   
  Program.Delay(5)
EndWhile

' Creates an array to hold the apple shapes
Sub AddApples	
  For aplNum = 1 To MAX_APPLES
    apple[aplNum] = Shapes.AddImage(path + "\Apple.png")
    scale = (3 + Math.GetRandomNumber(5)) / 10
    Shapes.Zoom(apple[aplNum], scale, scale)    
    SetApplePosition()
  EndFor
EndSub

' Sets a random position for apple[aplNum]
Sub SetApplePosition
  xPos = Math.GetRandomNumber(420)
  yPos = -Math.GetRandomNumber(500)
  Shapes.Move(apple[aplNum], xPos, yPos)
EndSub

' Moves the apples and checks if they were caught
Sub MoveApples
  For aplNum = 1 To MAX_APPLES
    xPos = Shapes.GetLeft(apple[aplNum])
    yPos = Shapes.GetTop (apple[aplNum])   
    Shapes.Move(apple[aplNum], xPos, yPos + 1)
    
    CheckCatch() ' Checks if the apple landed in the cart
    If (gotIt = 1) Then
      Sound.PlayClick()
      numCaught = numCaught + 1
      SetApplePosition()
    ElseIf (yPos > 320) Then
      numMissed = numMissed + 1
      SetApplePosition()      
    EndIf
  EndFor 
  
  msg = "Caught: " + numCaught + "   Missed: " + numMissed
  Shapes.SetText(msgID, msg)  
EndSub  

' Checks the distance between apple[aplNum] and the cart
Sub CheckCatch
  xApple = Shapes.GetLeft(apple[aplNum]) + 32 ' Center point
  yApple = Shapes.GetTop(apple[aplNum]) + 32  ' Bottom point
  xCart = Shapes.GetLeft(cartImg) + 50        ' Center point
  yCart = Shapes.GetTop(cartImg) + 40         ' Around the center
  xdiff = Math.Abs(xApple - xCart)
  ydiff = Math.Abs(yApple - yCart)
  gotIt = 0    ' Assumes we didn't get the apple
  If ((xdiff < 20) And (ydiff < 20)) Then
    gotIt = 1  ' We got it
  EndIf
EndSub