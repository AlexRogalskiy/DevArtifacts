' AreaCalculator.sb
' Learn to Program with Small Basic. Copyright (c) 2016 by Majed Marji and Ed Price
' Calculates the area of different geometric shapes

GraphicsWindow.Title = "Area Calculator"
TextWindow.Title = GraphicsWindow.Title
GraphicsWindow.Width = 480
GraphicsWindow.Height = 360
GraphicsWindow.CanResize = 0

GetChoice:
bkgndImg = Program.Directory + "\MainMenu.png"
GraphicsWindow.DrawImage(bkgndImg, 0, 0)

TextWindow.Write("Enter your choice [1-3]: ")
choice = TextWindow.ReadNumber()

If (choice = 1) Then
  TriangleAreaManager()
ElseIf (choice = 2) Then
  SquareAreaManager()
ElseIf (choice = 3) Then
  CircleAreaManager()
Else
  TextWindow.WriteLine("Sorry, that’s not an option.")
EndIf

TextWindow.WriteLine("")
Goto GetChoice

Sub TriangleAreaManager
  bkgndImg = Program.Directory + "\TriMenu.png"
  GraphicsWindow.DrawImage(bkgndImg, 0, 0)
  
  TextWindow.Write("Triangle choice [1-3]: ")
  choice = TextWindow.ReadNumber()
  
  If (choice = 1) Then
    Tri1Area()
  ElseIf (choice = 2) Then
    Tri2Area()
  ElseIf (choice = 3) Then
    Tri3Area()
  Else
    TextWindow.WriteLine("Sorry, that’s not an option.")
  EndIf
EndSub

Sub Tri1Area
  TextWindow.Write("Enter the base: ")
  b = TextWindow.ReadNumber()
  TextWindow.Write("Enter the height: ")
  h = TextWindow.ReadNumber()
  area = 0.5 * b * h
  TextWindow.WriteLine("Area = " + area) 
EndSub

Sub Tri2Area
  TextWindow.Write("Enter the base: ")
  b = TextWindow.ReadNumber()
  h = Math.SquareRoot(0.75) * b 
  area = 0.5 * b * h
  TextWindow.WriteLine("Area = " + area)
EndSub

Sub Tri3Area
  TextWindow.Write("Enter the base: ")
  b = TextWindow.ReadNumber()
  TextWindow.Write("Enter the side-length: ")
  s = TextWindow.ReadNumber()
  h = Math.SquareRoot((s * s) - ((b / 2) * (b / 2)))
  area = 0.5 * b * h
  TextWindow.WriteLine("Area = " + area)
EndSub

Sub SquareAreaManager
  bkgndImg = Program.Directory + "\SquareMenu.png"
  GraphicsWindow.DrawImage(bkgndImg, 0, 0)
  
  TextWindow.Write("Square choice [1-2]: ")
  choice = TextWindow.ReadNumber()
  
  If (choice = 1) Then
    TextWindow.Write("Enter the side-length: ")
    s = TextWindow.ReadNumber()
    area = s * s
    TextWindow.WriteLine("Area = " + area)  
  ElseIf (choice = 2 ) Then
    TextWindow.Write("Enter the diagonal: ")
    d = TextWindow.ReadNumber()
    s = Math.SquareRoot(2) * d
    area = s * s
    TextWindow.WriteLine("Area = " + area)    
  Else
    TextWindow.WriteLine("Sorry, that’s not an option.")
  EndIf
EndSub

Sub CircleAreaManager
  bkgndImg = Program.Directory + "\CircleMenu.png"
  GraphicsWindow.DrawImage(bkgndImg, 0, 0)
  
  TextWindow.Write("Circle choice [1-2]: ")  
  choice = TextWindow.ReadNumber()
  
  If ((choice = 1) Or (choice = 2)) Then
    CircleAreaCalculator()
  Else
    TextWindow.WriteLine("Sorry, that’s not an option.")
  EndIf
EndSub

Sub CircleAreaCalculator
  If (choice = 1) Then
    TextWindow.Write("Enter the radius: ")
    r = TextWindow.ReadNumber()
  Else
    TextWindow.Write("Enter the circumference: ")
    c = TextWindow.ReadNumber()
    r = c / (2 * Math.Pi)
  EndIf
  
  area = r * r * Math.Pi
  TextWindow.WriteLine("Area = " + area)
EndSub