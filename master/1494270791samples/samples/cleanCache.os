#Использовать logos

Перем ЖурналЛогирования;


//**
//* Процедура выполняет очистку кэша на основании переданных параметров
//*
//* @param {Структура}
//*
Процедура ВыполнитьОчистикуКэша(Параметры)

	Для Каждого ДиректорияОбработки Из Параметры.ДиректорииДляОбработки Цикл

		ЖурналЛогирования.Информация("----- Очистка каталога: " + ДиректорияОбработки);

		РабочийКаталог = Новый Файл(ДиректорияОбработки);
		
		Если НЕ РабочийКаталог.Существует() Тогда
			ЖурналЛогирования.Ошибка("!!! Директория " + ДиректорияОбработки + " не существует. Пропускаем.");
			Продолжить;
		КонецЕсли;

		ФайлыДляУдаления = НайтиФайлы(ДиректорияОбработки, "*", Ложь);

		Для Каждого ОбнаруженныйФайл Из ФайлыДляУдаления Цикл

			Если ОбнаруженныйФайл.ЭтоКаталог() И Параметры.Исключения.Найти(ОбнаруженныйФайл) <> Неопределено Тогда
				ЖурналЛогирования.Информация("----- Совпало исключение. Пропускаем");
				Продолжить;
			КонецЕсли;

			Попытка
				УдалитьФайлы(ОбнаруженныйФайл.ПолноеИмя);				
			Исключение
				ЖурналЛогирования.Ошибка("!!! Не удалось удалить директорию: " + ОбнаруженныйФайл.ПолноеИмя);				
			КонецПопытки;

		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

//**
//* Инициализация параметров обработки. Сюда можно записать
//* дополнительные пути для анализац.
//*
//* @return {Структура.<ДиректорииДляОбработки, Исключения>}
Функция ПодготовитьПараметры()

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ДиректорияПрофиляПользователя = СистемнаяИнформация.ПолучитьПеременнуюСреды("USERPROFILE");

	Результат = Новый Структура();
	Результат.Вставить("ДиректорииДляОбработки", Новый Массив);
	Результат.Вставить("Исключения", Новый Массив);

	Результат.ДиректорииДляОбработки.Добавить(ДиректорияПрофиляПользователя + "\Application Data\1C\1Cv8");
	Результат.ДиректорииДляОбработки.Добавить(ДиректорияПрофиляПользователя + "\Local Settings\Application Data\1C\1Cv8");
	Результат.ДиректорииДляОбработки.Добавить(ДиректорияПрофиляПользователя + "\Local Settings\Application Data\1C\1Cv82");
	Результат.ДиректорииДляОбработки.Добавить(ДиректорияПрофиляПользователя + "\Application Data\1C\1Cv82");

	Результат.Исключения.Добавить("ExtCompT");

	Возврат Результат;

КонецФункции

ЖурналЛогирования = Логирование.ПолучитьЛог("oscript.app.cleaner");
Параметры = ПодготовитьПараметры();

ВыполнитьОчистикуКэша(Параметры);

