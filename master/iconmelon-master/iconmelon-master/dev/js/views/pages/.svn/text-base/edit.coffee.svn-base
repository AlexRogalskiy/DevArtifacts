define 'views/pages/edit', [ 	'views/pages/PageView'
								'text!themes'
								'underscore'
								'views/Constructor'
							 	'models/constructor-model'
							 	'views/notifier'
							],( PageView
								themesText
								_
								Constructor
								ConstructorModel
								notifier
								)->

	class Edit extends PageView
		template: '#edit-page-template'

		events: 
			'change #js-select-place': 'changeTheme'
			'click  #js-add-icon-collection': 'addIconCollection'


		initialize:(@o={})->
			if !App.themes
				App.themes = $.parseJSON themesText
				App.themes = $.parseJSON App.themes.data

			$('#js-generate').show()

			$('.js-nav-link').eq(1).addClass 'is-check'
			@

		render:->
			super
			@renderInit()
			@

		renderInit:->
			@renderSelect()

		renderSelect:->
			options = ''
			i = 0
			for theme of App.themes
				# !i++ and
				i++ or (@currentThemeName = theme)
				options += "<option value='#{ theme }'> #{ theme } </option>"

			@$('#js-select-place').html options
			@showCurrentTheme()

		changeTheme:->
			@currentThemeName 	= @$('#js-select-place').find(':selected').val()
			@showCurrentTheme()

		showCurrentTheme:(type)->
			@initFileUpload()
			
			@currentTheme 		= App.themes[@currentThemeName]
			_.defer =>
				@constructorModel 	= null
				@constructor 		= null

				if @currentTheme?

					@constructorModel = new ConstructorModel
											url: @currentTheme.image
											themes: App.themes

					@constructor = new Constructor 
											model: @constructorModel
											mode: 	'edit'
											theme: 	@currentTheme
											type: 	type

					App.addRegions
						constructor: '#js-constructor-region'

					App.constructor.show @constructor

				else notifier.add 
						text: 'There is no data to edit'
						type: 'warning'

		initFileUpload:->
			@$('#fileupload').fileupload
				url: 'php/'
				acceptFileTypes: /(\.|\/)(gif|jpe?g|png)$/i
				dataType: 'json'
				limitMultiFileUploads: 1
				done:(e, data)=>
					@loadImage(@currentTheme.image).then (oldImg)=>
						@loadImage(data.result.files[0].url).then (newImg)=>
							if oldImg.width > newImg.width or oldImg.height > newImg.height
								if confirm 'New image is smaller than previous, so data can be lost. Are you sure to proceed?'
									@changeImage data
							else @changeImage data
					
				progressall:(e, data)=>
					console.log 'progress'
					false

			@

		changeImage:(data)->
			@currentTheme.image = data.result.files[0].url
			@showCurrentTheme 'change-image'

		loadImage:(image)->
			dfr = new $.Deferred
			@$img = $('<img>').on 'load', ->
				dfr.resolve @
			@$img[0].src = image
			dfr.promise()

		addIconCollection:->
			model = App.iconsCollectionWrapped.at App.iconsCollectionWrapped.length-1
			App.iconsCollectionWrapped.add model.toJSON()


	Edit















